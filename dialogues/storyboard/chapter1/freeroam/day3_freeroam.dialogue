~ nextDay
Vuoi andare a dormire?
- Vai a letto
	luka: Penso sia ora di andare a dormire.
	set GameState.story_continue = "res://dialogues/storyboard/chapter1/day2/after_freeroam.dialogue"
	set GameState.continued_from_night = true
	do GameState.change_scene("res://scenes/chapter1/chapter1.tscn")
- Aspetta
	luka: Vorrei controllare ancora qualcosa.
=> END

~ bocia
if GameState.day3_bocia_talk
	Il Bocia, sta giocando a sasso carta forbice con Nele.
	Sembra stia vincendo.
else
	luka: Ciao Piccola, sei pronta per domani?
	bocia: SIIIII NON VEDO L'ORA DI FARE PEW PEW PEW
	luka: ehm, ecco a proposito, te domani avrai il compito più arduo di tutti.
	Gli occhi del bocia si ingigantiscono fissandomi.
	luka: Dovrai tenere al sicuro il campo, qualsiasi cattivone dovesse avvicinarsi, dovrai prenderlo a calci nel sedere!
	bocia: Quindi... Non verrò con voi...
	Sembra quasi si stia per mettere a piangere...
	bocia: EVVAI, Non preoccuparti capo puzzoso cattivone, la difenderò io la base!
	Meno male.
	luka: Brava ragazza, mi raccomando, contiamo su di te.
	il Bocia si scaraventa su di me abbracciandomi
	do GameState.wait()
	if GameState.day2_bocia_quest_done
		bocia: Prendi questo, ti proteggerà!
		In mano aveva il trenino che le avevo trovato il giorno prima.
		- Prendilo
			luka: Grazie Bocia, lo terrò con me finchè non torneremo.
			bocia: Mi raccomando, non perderlo! Ci tengo molto!
			Prendendolo e guardandolo meglio, noto come un pezzo di nastro di carta sul fondo del trenino.
			C'è un numero segnato, scritto a penna...
			Sembra stato scritto frettolosamente, inoltre l'inchiostro è tutto sbavato, probabilmente a causa della neve bagnata.
			8090? Mi chiedo a cosa possa servire...
		- Non prenderlo
			luka: Grazie, ma penso sia meglio che lo tenga tu.
			luka: Potrebbe proteggerti dai cattivoni!
			bocia: mmmmmmm
			bocia: Ok! Lo terrò io!
			Le scompiglio i capelli amorevolmente.
			luka: Brava piccola.
=> END

~ nele
if GameState.day3_nele_talk
	Nele, sta giocando a sasso carta forbice con il Bocia.
	Sembra stia perdendo alla grande.
else
	Aggiungere dialogo mancante
=> END

~ tent
La mia terrificante tenda da notte.
Non è comoda come quella che abbiamo al campo, ma perlomento è qualcosa.
=> END

~ soldati
La mia scrivania, disordinata come sempre.
=> END

~ warplan
if GameState.petr_alive
	if GameState.day3_petr_talk
		Petr è occupato a scarabocchiare su un documento.
	else
		set GameState.day3_petr_talk = true
		Petr sta rileggendo freneticamente un fascicolo che non avevo mai visto.
		luka: Petr!
		petr: AHHHHH!
		luka: ...
		petr: ...
		petr: Ciao Luka, scusami, mi hai spaventato a morte.
		luka: Cerca di tenere basso il tono di voce, miracolato.
		luka: Che stai combinando? Non penso di aver mai visto quel... coso che hai in mano.
		petr: Questo? Sto rivalutando tutto quello che sappiamo sulla centrale, con Nele prima ho studiato un piano d'azione.
		petr: Vuoi vederlo?
		- Dagli un'occhiata
			luka: Dimmi tutto, ti ascolto.
			petr: Allora, come ho detto prima potremmo provare a passare per la grata trasandata, passare di lì dovrebbe essere molto semplice.
			petr: Dovremmo trovare una distrazione, pensavamo di mandare qualcuno all'entrata con un camion, che li distrarrà per un po'.
			luka: E chi mi dice che questi non ci spareranno a vista?
			petr: Spesso arrivavano camion di diverso tipo dal governo, non sanno mai chi possa arrivare e con che cosa.
			petr: Il che ci può dare un po' di tempo. Guarda, qui ho un po' di documenti falsi che ho fatto in questi giorni.
			petr: Sono molto simili a quelli che usavano loro, ci metteranno del tempo a capire il trucco.
			luka: Rimane comunque molto rischioso.
			petr: ...è il massimo che possiamo fare.
			petr: Nel mentre, gli altri passeranno da dietro, nel condotto, e prendendo la via a destra, sbucheranno nei sotterranei.
			petr: Lì non c'è quasi nessuno, al massimo una guardia affamata, potrete farla fuori tranquillamente con un arma silenziata.
			petr: Una volta usciti, proseguendo per il corridoio, sulla sinistra c'è la sala di controllo, controlliamo se è aperta.
			petr: Da lì potremo prendere controllo delle torrette, e ripulire il campo in un attimo.
			petr: Altimenti, dovremo disattivare il generatore, nella sala macchine più avanti.
			petr: Una volta fatto, non potranno chiedere aiuto, ed essendo molti di meno, la vinceremo facile.
			luka: La stai facendo tu molto facile, sei sicuro di quello che dici?
			petr: Assolutamente, vedrai, la centrale sarà nostra in un battibaleno.
			luka: Grazie Petr. Sono contento tu sia con noi.
		- Lascia perdere
			luka: Per stasera passo, mi fido di voi.
			luka: Si è fatto tardi, dovresti andare a riposare anche te.
elif GameState.petr_alive == false
	if GameState.day3_petr_talk
		Due fascicoli giaciono sul tavolo.
	else
		set GameState.day3_petr_talk = true
		Due fascicoli giaciono sul tavolo.
		luka: Nele! Cosa sono questi?
		nele: Mh? Ah, questo è il fascicolo che ci ha lasciato Petr, questo invece l'ha scritto Ivan.
		nele: Ha detto di aver scritto un piano d'attacco, non sono proprio convinta, ma dovresti leggerlo anche te.
		Piano di attacco di ivan va qui.	
=> END
