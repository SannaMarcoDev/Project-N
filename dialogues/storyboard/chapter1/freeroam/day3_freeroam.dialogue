~ nextDay
Vuoi andare a dormire?
- Vai a letto
	luka: Penso sia ora di andare a dormire.
	set GameState.story_continue = "res://dialogues/storyboard/chapter1/day3/after_freeroam.dialogue"
	set GameState.continued_from_night = true
	do GameState.change_scene("res://scenes/chapter1/chapter1.tscn")
- Aspetta
	luka: Vorrei controllare ancora qualcosa.
=> END

~ bocia
if GameState.day3_bocia_talk
	Braciola, sta giocando a sasso carta forbice con Nele.
	Sembra stia vincendo.
else
	set GameState.day3_bocia_talk = true
	luka: Ciao Piccola, sei pronta per domani?
	Braciola: SIIIII NON VEDO L'ORA DI FARE PEW PEW PEW
	luka: ehm, ecco a proposito, te domani avrai il compito più arduo di tutti.
	Gli occhi del Braciola si ingigantiscono fissandomi.
	luka: Dovrai tenere al sicuro il campo, qualsiasi cattivone dovesse avvicinarsi, dovrai prenderlo a calci nel sedere!
	Braciola: Quindi... Non verrò con voi...
	Sembra quasi si stia per mettere a piangere...
	Braciola: EVVAI, Non preoccuparti capo puzzoso cattivone, la difenderò io la base!
	Meno male.
	luka: Brava ragazza, mi raccomando, contiamo su di te.
	Braciola si scaraventa su di me abbracciandomi
	do GameState.wait()
	if GameState.day2_bocia_quest_done
		Braciola: Prendi questo, ti proteggerà!
		In mano aveva il trenino che le avevo trovato il giorno prima.
		- Prendilo
			luka: Grazie Braciola, lo terrò con me finchè non torneremo.
			Braciola: Mi raccomando, non perderlo! Ci tengo molto!
			Prendendolo e guardandolo meglio, noto come un pezzo di nastro di carta sul fondo del trenino.
			C'è un numero segnato, scritto a penna...
			Sembra stato scritto frettolosamente, inoltre l'inchiostro è tutto sbavato, probabilmente a causa della neve bagnata.
			8090? Mi chiedo a cosa possa servire...
			luka: Grazie, ti prometto che te lo riporterò tutto intero!
			Le scompiglio i capelli amorevolmente.
		- Non prenderlo
			luka: Grazie, ma penso sia meglio che lo tenga tu.
			luka: Potrebbe proteggerti dai cattivoni!
			Braciola: mmmmmmm
			Braciola: Ok! Lo terrò io!
			Le scompiglio i capelli amorevolmente.
			luka: Brava piccola.
=> END

~ nele
if GameState.day3_nele_talk
	Nele, sta giocando a sasso carta forbice con Braciola.
	Sembra stia perdendo alla grande.
else
	set GameState.day3_nele_talk = true
	luka: Nele!
	nele: SHHHHHH!
	luka: ...
	nele: Non vedi che sto giocando con Braciola?
	luka: Si... Lo vedo...
	nele: Scusa Braciola, dacci 5 minuti.
	luka: Nono, figurati, divertitevi, e mi raccomando Braciola, stendila!
	luka: Tanto ci vediamo in tenda più tardi.
	Nele mi guarda compiaciuta, sapeva che è un momento importante, ma sta facendo di tutto per non farlo pesare alla bimba.
	Dopotutto lei coi bambini ci ha sempre saputo fare.
=> END

~ tent
La mia terrificante tenda da notte.
Non è comoda come quella che abbiamo al campo, ma perlomento è qualcosa.
=> END

~ soldati
if GameState.stole_food
	I soldati si stanno ubriacando con quel poco vino che abbiamo trovato nei camion.
	Sono allegri, pieni di energie.
	Fanno finta di fare a botte, come fosse un provino cinematografico.
	Non vedono l'ora di avere una grande vittoria domani.
else
	I soldati sono a terra. Nessuno fiata.
	Stanno tutti cercando di mantenere le loro energie, nessuno spreco.
	Per fortuna hanno trovato qualche coniglio nei dintorni, che sono riusciti a dividersi.
	Ma non tutti sono riusciti a prendere una parte, si sono dovuti accontentare delle gallette.
	Sperano solo di avere una vittoria domani.
	E di conquistare la loro mensa.
=> END

~ warplan
if GameState.petr_alive
	if GameState.day3_petr_talk
		Petr è occupato a scarabocchiare su un documento.
	else
		set GameState.day3_petr_talk = true
		Petr sta rileggendo freneticamente un fascicolo che non avevo mai visto.
		luka: Petr!
		petr: AHHHHH!
		luka: ...
		petr: ...
		petr: Ciao Luka, scusami, mi hai spaventato a morte.
		luka: Cerca di tenere basso il tono di voce, miracolato.
		luka: Che stai combinando? Non penso di aver mai visto quel... coso che hai in mano.
		petr: Questo? Sto rivalutando tutto quello che sappiamo sulla centrale, con Nele prima ho studiato un piano d'azione.
		petr: Vuoi vederlo?
		- Dagli un'occhiata
			luka: Dimmi tutto, ti ascolto.
			petr: Allora, come ho detto prima potremmo provare a passare per la grata trasandata, passare di lì dovrebbe essere molto semplice.
			petr: Dovremmo trovare una distrazione, pensavamo di mandare qualcuno all'entrata con un camion, che li distrarrà per un po'.
			luka: E chi mi dice che questi non ci spareranno a vista?
			petr: Spesso arrivavano camion di diverso tipo dal governo, non sanno mai chi possa arrivare e con che cosa.
			petr: Il che ci può dare un po' di tempo. Guarda, qui ho un po' di documenti falsi che ho fatto in questi giorni.
			petr: Sono molto simili a quelli che usavano loro, ci metteranno del tempo a capire il trucco.
			luka: Rimane comunque molto rischioso.
			petr: ...è il massimo che possiamo fare.
			petr: Nel mentre, gli altri passeranno da dietro, nel condotto, e prendendo la via a destra, sbucheranno nei sotterranei.
			petr: Lì non c'è quasi nessuno, al massimo una guardia, potrete farla fuori tranquillamente con un arma silenziata.
			petr: Una volta usciti, proseguendo per il corridoio, sulla sinistra c'è la sala di controllo, controlliamo se è aperta.
			petr: Da lì potremo prendere controllo delle torrette, e ripulire il campo in un attimo.
			petr: Altimenti, dovremo disattivare il generatore, nella sala macchine più avanti.
			petr: Una volta fatto, non potranno chiedere aiuto, ed essendo molti di meno, la vinceremo facile.
			luka: La stai facendo tu molto facile, sei sicuro di quello che dici?
			petr: Assolutamente, vedrai, la centrale sarà nostra in un battibaleno.
			luka: Grazie Petr. Sono contento tu sia con noi.
		- Lascia perdere
			luka: Per stasera passo, mi fido di voi.
			luka: Si è fatto tardi, dovresti andare a riposare anche te.
elif GameState.petr_alive == false
	if GameState.day3_petr_talk
		Due fascicoli giaciono sul tavolo.
	else
		set GameState.day3_petr_talk = true
		Due fascicoli giaciono sul tavolo.
		luka: Nele! Cosa sono questi?
		nele: Mh? Ah, questo è il fascicolo che ci ha lasciato Petr, questo invece l'ha scritto Ivan.
		nele: Ha detto di aver scritto un piano d'attacco, non sono proprio convinta, ma dovresti leggerlo anche te.
		Stando a quanto scritto nel fascicolo di Ivan, il piano è molto semplice, rudimentale oserei dire.
		In pratica, dovremo lanciare un camion con degli esplosivi, all'entrata principale, e al contempo alcuni forniranno fuoco di copertura.
		Il tutto è in realtà una distrazione, una seconda squadra, meno numerosa, passerà dalle fogne.
		Non sarà un granchè, dato il freddo e la puzza, ma perlomeno dovremmo riuscire a entrare.
		Dopodichè, sbucheremo nei sotterranei, non sappiamo quanta gente possa esserci, quindi dovremo essere pronti a tutto.
		Preso il controllo dei sotterranei, uscendo dalla porta e andando a destra, in fondo, dovremmo trovare una sala di controllo.
		Potrebbe essere chiusa, nel caso, la porta successiva è la sala macchine, basterà spegnere il generatore per aprire la porta.
		Presa quella sala, avremo tutto il campo sotto controllo, con telecamere e comandi a distanza di porte e torrette.
		Non resterà altro che eliminare i nemici, e il gioco sarà fatto.
		nele: Bhe mi sembra tutto chiaro.
		nele: Non sarà il miglior piano, ma a me non viene in mente molto altro.
		nele: Che ne dici?
		Uhmm...
		- Controlla la piantina
			luka: Nele mi passeresti la piantina? Vorrei capire bene come è strutturato il posto.
			nele: Piantina? Non c'è nessuna piantina che io sappia?
			luka: In che senso scusa? Ricordo chiaramente una piantina della centrale.
			luka: Come avrebbe fatto Ivan a fare un piano senza?
			nele: Ha usato come riferimento i log delle telecamere, sono più di tremila pagine.
			nele: Sono lì dentro, ma ci vorrà un po' a capire tutto.
			luka: ...
			luka: Capisco...
			nele: Tutto bene? Sembri pallido.
			luka: No, no. Va tutto bene.
			luka: Devo essermi immaginato qualche documento in più.
			Sono impazzito? Ero sicuro che ci fosse una piantina.
			Forse non ho più l'età per fare il comandante.
			Vorrei solo tornare a casa...
		- Va bene così
			luka: No direi che va bene.
			luka: Mi fido di Ivan, se questo è il meglio che ha tirato fuori, allora faremo così.
=> END
